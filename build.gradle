plugins {
    id 'com.github.hierynomus.license' version '0.11.0'  // https://github.com/hierynomus/license-gradle-plugin
    id 'com.github.ben-manes.versions' version '0.10.1'
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'license'

version = '0.0.1-SNAPSHOT'

def fileEncoding = 'UTF-8'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets.main.java.srcDirs = ['src/main/java']
sourceSets.test.java.srcDirs = []
sourceSets.main.groovy.srcDirs = []
sourceSets.test.groovy.srcDirs = ['src/test/groovy']

mainClassName = 'ch.petikoch.examples.feedbackControlInJava.FeedbackControlInJavaMain'

repositories {
    jcenter()
}

def rxJavaVersion = '1.0.+'
def apacheCommonsMath3Version = '3.5'
def jFreeChartVersion = '1.0.19'
def googleGuavaVersion = '18.0'
def jcipVersion = '1.0'
def javaslangVersion = '1.2.+'
def groovyVersion = '2.4.3'
def spockVersion = '1.0-groovy-2.4'
def cglibVersion = '3.1'

dependencies {
    compile "org.codehaus.groovy:groovy-all:${groovyVersion}" // Apache 2.0 licence
    compile "org.jfree:jfreechart:${jFreeChartVersion}" // LGPL V2 licence
    compile "org.apache.commons:commons-math3:${apacheCommonsMath3Version}" // Apache 2.0 licence
    compile "com.google.guava:guava:${googleGuavaVersion}" // Apache 2.0 licence
    compile "net.jcip:jcip-annotations:${jcipVersion}" // http://creativecommons.org/licenses/by/2.5
    compile "io.reactivex:rxjava:${rxJavaVersion}" // Apache 2.0 licence
    compile "com.javaslang:javaslang:${javaslangVersion}" // Apache 2.0 licence

    testCompile("org.spockframework:spock-core:${spockVersion}") { // Apache 2.0 licence
        exclude group: 'org.codehaus.groovy'
    }
    testCompile "cglib:cglib-nodep:${cglibVersion}" // spock mocks // Apache 2.0 licence
}

test {
    systemProperty 'file.encoding', fileEncoding
}

tasks.withType(GroovyCompile) {
    groovyOptions.encoding = fileEncoding
    options.encoding = fileEncoding
}

tasks.withType(JavaCompile) {
    options.encoding = fileEncoding
}

// http://forums.gradle.org/gradle/topics/set_maxparallelforks_to_number_of_cores_on_the_current_machine
tasks.withType(Test) {
    maxParallelForks = Runtime.getRuntime().availableProcessors()
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion
    }
}

license {
    header = rootProject.file('config/HEADER_apache_license2.txt')
    strictCheck = true
}